module S4WithoutTerms where

open import Prelude public


--------------------------------------------------------------------------------
--
-- Syntax


-- Type variables
data TVar : Set where
  tvar : String Рєњ TVar

injtvar : Рѕђ {sРѓЂ sРѓѓ} Рєњ tvar sРѓЂ РЅА tvar sРѓѓ Рєњ sРѓЂ РЅА sРѓѓ
injtvar refl = refl

_РЅЪРѓюрхЦ_ : (xРѓЂ xРѓѓ : TVar) Рєњ Dec (xРѓЂ РЅА xРѓѓ)
tvar sРѓЂ РЅЪРѓюрхЦ tvar sРѓѓ = mapDec (tvar &_) injtvar (sРѓЂ РЅЪРѓЏ sРѓѓ)

instance
  tvarIsString : IsString TVar
  tvarIsString =
    record
      { Constraint = ╬╗ s Рєњ Ріц
      ; fromString = ╬╗ s Рєњ tvar s
      }


-- Types
infixl 9 _РѕД_
infixr 7 _РіЃ_
data Tp : Set where
  рхЌрхЏ  : (x : TVar) Рєњ Tp
  _РіЃ_ : (A B : Tp) Рєњ Tp
  _РѕД_ : (A B : Tp) Рєњ Tp
  ­ЮћЌ   : Tp
  РќА_  : (A : Tp) Рєњ Tp

instance
  typeIsString : IsString Tp
  typeIsString =
    record
      { Constraint = ╬╗ s Рєњ Ріц
      ; fromString = ╬╗ s Рєњ рхЌрхЏ (tvar s)
      }


-- Contexts
Cx : Set
Cx = List┬▓ Tp Tp


-- Syntactic entailment
infix 3 _Ріб_
data _Ріб_ : Cx Рєњ Tp Рєњ Set
  where
    рхљрхЏ  : Рѕђ {A ╬ћ ╬Њ} Рєњ (i : ╬ћ РѕІ A)
                    Рєњ ╬ћ РЂЈ ╬Њ Ріб A

    рхЏ   : Рѕђ {A ╬ћ ╬Њ} Рєњ (i : ╬Њ РѕІ A)
                    Рєњ ╬ћ РЂЈ ╬Њ Ріб A

    кЏ   : Рѕђ {A B ╬ћ ╬Њ} Рєњ (­ЮњЪ : ╬ћ РЂЈ ╬Њ , A Ріб B)
                      Рєњ ╬ћ РЂЈ ╬Њ Ріб A РіЃ B

    _$_ : Рѕђ {A B ╬ћ ╬Њ} Рєњ (­ЮњЪ : ╬ћ РЂЈ ╬Њ Ріб A РіЃ B) (Рё░ : ╬ћ РЂЈ ╬Њ Ріб A)
                      Рєњ ╬ћ РЂЈ ╬Њ Ріб B

    _,_ : Рѕђ {A B ╬ћ ╬Њ} Рєњ (­ЮњЪ : ╬ћ РЂЈ ╬Њ Ріб A) (Рё░ : ╬ћ РЂЈ ╬Њ Ріб B)
                      Рєњ ╬ћ РЂЈ ╬Њ Ріб A РѕД B

    ¤ђРѓЂ  : Рѕђ {A B ╬ћ ╬Њ} Рєњ (­ЮњЪ : ╬ћ РЂЈ ╬Њ Ріб A РѕД B)
                      Рєњ ╬ћ РЂЈ ╬Њ Ріб A

    ¤ђРѓѓ  : Рѕђ {A B ╬ћ ╬Њ} Рєњ (­ЮњЪ : ╬ћ РЂЈ ╬Њ Ріб A РѕД B)
                      Рєњ ╬ћ РЂЈ ╬Њ Ріб B

    tt  : Рѕђ {╬ћ ╬Њ} Рєњ ╬ћ РЂЈ ╬Њ Ріб ­ЮћЌ

    Рїю_РїЮ : Рѕђ {A ╬ћ ╬Њ} Рєњ (­ЮњЪ : ╬ћ РЂЈ РѕЁ Ріб A)
                    Рєњ ╬ћ РЂЈ ╬Њ Ріб РќА A

    Рїъ_РїЪ : Рѕђ {A C ╬ћ ╬Њ} Рєњ (­ЮњЪ : ╬ћ РЂЈ ╬Њ Ріб РќА A) (Рё░ : ╬ћ , A РЂЈ ╬Њ Ріб C)
                      Рєњ ╬ћ РЂЈ ╬Њ Ріб C


-- Normal and neutral forms
mutual
  infix 3 _РібРѓЎРѓў_
  data _РібРѓЎРѓў_ : Cx Рєњ Tp Рєњ Set
    where
      кЏ   : Рѕђ {A B ╬ћ ╬Њ} Рєњ (­ЮњЪ : ╬ћ РЂЈ ╬Њ , A РібРѓЎРѓў B)
                        Рєњ ╬ћ РЂЈ ╬Њ РібРѓЎРѓў A РіЃ B

      _,_ : Рѕђ {A B ╬ћ ╬Њ} Рєњ (­ЮњЪ : ╬ћ РЂЈ ╬Њ РібРѓЎРѓў A) (Рё░ : ╬ћ РЂЈ ╬Њ РібРѓЎРѓў B)
                        Рєњ ╬ћ РЂЈ ╬Њ РібРѓЎРѓў A РѕД B

      tt  : Рѕђ {╬ћ ╬Њ} Рєњ ╬ћ РЂЈ ╬Њ РібРѓЎРѓў ­ЮћЌ

      Рїю_РїЮ : Рѕђ {A ╬ћ ╬Њ} Рєњ (­ЮњЪ : ╬ћ РЂЈ РѕЁ Ріб A)
                      Рєњ ╬ћ РЂЈ ╬Њ РібРѓЎРѓў РќА A

      Рїъ_РїЪ : Рѕђ {A C ╬ћ ╬Њ} Рєњ (­ЮњЪ : ╬ћ РЂЈ ╬Њ РібРѓЎРѓю РќА A) (Рё░ : ╬ћ , A РЂЈ ╬Њ РібРѓЎРѓў C)
                        Рєњ ╬ћ РЂЈ ╬Њ РібРѓЎРѓў C

      РЂ┐рхЌ  : Рѕђ {x ╬ћ ╬Њ} Рєњ (­ЮњЪ : ╬ћ РЂЈ ╬Њ РібРѓЎРѓю рхЌрхЏ x)
                      Рєњ ╬ћ РЂЈ ╬Њ РібРѓЎРѓў рхЌрхЏ x

  infix 3 _РібРѓЎРѓю_
  data _РібРѓЎРѓю_ : Cx Рєњ Tp Рєњ Set
    where
      рхљрхЏ  : Рѕђ {A ╬ћ ╬Њ} Рєњ (i : ╬ћ РѕІ A)
                      Рєњ ╬ћ РЂЈ ╬Њ РібРѓЎРѓю A

      рхЏ   : Рѕђ {A ╬ћ ╬Њ} Рєњ (i : ╬Њ РѕІ A)
                      Рєњ ╬ћ РЂЈ ╬Њ РібРѓЎРѓю A

      _$_ : Рѕђ {A B ╬ћ ╬Њ} Рєњ (­ЮњЪ : ╬ћ РЂЈ ╬Њ РібРѓЎРѓю A РіЃ B) (Рё░ : ╬ћ РЂЈ ╬Њ РібРѓЎРѓў A)
                        Рєњ ╬ћ РЂЈ ╬Њ РібРѓЎРѓю B

      ¤ђРѓЂ  : Рѕђ {A B ╬ћ ╬Њ} Рєњ (­ЮњЪ : ╬ћ РЂЈ ╬Њ РібРѓЎРѓю A РѕД B)
                        Рєњ ╬ћ РЂЈ ╬Њ РібРѓЎРѓю A

      ¤ђРѓѓ  : Рѕђ {A B ╬ћ ╬Њ} Рєњ (­ЮњЪ : ╬ћ РЂЈ ╬Њ РібРѓЎРѓю A РѕД B)
                        Рєњ ╬ћ РЂЈ ╬Њ РібРѓЎРѓю B


mutual
  embРѓЎРѓў : Рѕђ {╬ћ ╬Њ A} Рєњ ╬ћ РЂЈ ╬Њ РібРѓЎРѓў A Рєњ ╬ћ РЂЈ ╬Њ Ріб A
  embРѓЎРѓў (кЏ ­ЮњЪ)     = кЏ (embРѓЎРѓў ­ЮњЪ)
  embРѓЎРѓў (­ЮњЪ , Рё░)   = embРѓЎРѓў ­ЮњЪ , embРѓЎРѓў Рё░
  embРѓЎРѓў tt        = tt
  embРѓЎРѓў (Рїю ­ЮњЪ РїЮ)   = Рїю ­ЮњЪ РїЮ
  embРѓЎРѓў (Рїъ ­ЮњЪ РїЪ Рё░) = Рїъ embРѓЎРѓю ­ЮњЪ РїЪ (embРѓЎРѓў Рё░)
  embРѓЎРѓў (РЂ┐рхЌ ­ЮњЪ)    = embРѓЎРѓю ­ЮњЪ

  embРѓЎРѓю : Рѕђ {╬ћ ╬Њ A} Рєњ ╬ћ РЂЈ ╬Њ РібРѓЎРѓю A Рєњ ╬ћ РЂЈ ╬Њ Ріб A
  embРѓЎРѓю (рхљрхЏ i)  = рхљрхЏ i
  embРѓЎРѓю (рхЏ i)   = рхЏ i
  embРѓЎРѓю (­ЮњЪ $ Рё░) = embРѓЎРѓю ­ЮњЪ $ embРѓЎРѓў Рё░
  embРѓЎРѓю (¤ђРѓЂ ­ЮњЪ)  = ¤ђРѓЂ (embРѓЎРѓю ­ЮњЪ)
  embРѓЎРѓю (¤ђРѓѓ ­ЮњЪ)  = ¤ђРѓѓ (embРѓЎРѓю ­ЮњЪ)


--------------------------------------------------------------------------------
--
-- Renaming


рхљren : Рѕђ {╬ћ ╬ћРђ▓ ╬Њ A} Рєњ ╬ћРђ▓ РіЄ ╬ћ Рєњ ╬ћ РЂЈ ╬Њ Ріб A
                    Рєњ ╬ћРђ▓ РЂЈ ╬Њ Ріб A
рхљren ╬и (рхљрхЏ i)    = рхљрхЏ (lookupрхБ ╬и i)
рхљren ╬и (рхЏ i)     = рхЏ i
рхљren ╬и (кЏ ­ЮњЪ)     = кЏ (рхљren ╬и ­ЮњЪ)
рхљren ╬и (­ЮњЪ $ Рё░)   = рхљren ╬и ­ЮњЪ $ рхљren ╬и Рё░
рхљren ╬и (­ЮњЪ , Рё░)   = рхљren ╬и ­ЮњЪ , рхљren ╬и Рё░
рхљren ╬и (¤ђРѓЂ ­ЮњЪ)    = ¤ђРѓЂ (рхљren ╬и ­ЮњЪ)
рхљren ╬и (¤ђРѓѓ ­ЮњЪ)    = ¤ђРѓѓ (рхљren ╬и ­ЮњЪ)
рхљren ╬и tt        = tt
рхљren ╬и (Рїю ­ЮњЪ РїЮ)   = Рїю рхљren ╬и ­ЮњЪ РїЮ
рхљren ╬и (Рїъ ­ЮњЪ РїЪ Рё░) = Рїъ рхљren ╬и ­ЮњЪ РїЪ (рхљren (lift ╬и) Рё░)

ren : Рѕђ {╬ћ ╬Њ ╬ЊРђ▓ A} Рєњ ╬ЊРђ▓ РіЄ ╬Њ Рєњ ╬ћ РЂЈ ╬Њ Ріб A
                   Рєњ ╬ћ РЂЈ ╬ЊРђ▓ Ріб A
ren ╬и (рхљрхЏ i)    = рхљрхЏ i
ren ╬и (рхЏ i)     = рхЏ (lookupрхБ ╬и i)
ren ╬и (кЏ ­ЮњЪ)     = кЏ (ren (lift ╬и) ­ЮњЪ)
ren ╬и (­ЮњЪ $ Рё░)   = ren ╬и ­ЮњЪ $ ren ╬и Рё░
ren ╬и (­ЮњЪ , Рё░)   = ren ╬и ­ЮњЪ , ren ╬и Рё░
ren ╬и (¤ђРѓЂ ­ЮњЪ)    = ¤ђРѓЂ (ren ╬и ­ЮњЪ)
ren ╬и (¤ђРѓѓ ­ЮњЪ)    = ¤ђРѓѓ (ren ╬и ­ЮњЪ)
ren ╬и tt        = tt
ren ╬и (Рїю ­ЮњЪ РїЮ)   = Рїю ­ЮњЪ РїЮ
ren ╬и (Рїъ ­ЮњЪ РїЪ Рё░) = Рїъ ren ╬и ­ЮњЪ РїЪ (ren ╬и Рё░)


mutual
  рхљrenРѓЎРѓў : Рѕђ {╬ћ ╬ћРђ▓ ╬Њ A} Рєњ ╬ћРђ▓ РіЄ ╬ћ Рєњ ╬ћ РЂЈ ╬Њ РібРѓЎРѓў A
                        Рєњ ╬ћРђ▓ РЂЈ ╬Њ РібРѓЎРѓў A
  рхљrenРѓЎРѓў ╬и (кЏ ­ЮњЪ)     = кЏ (рхљrenРѓЎРѓў ╬и ­ЮњЪ)
  рхљrenРѓЎРѓў ╬и (­ЮњЪ , Рё░)   = рхљrenРѓЎРѓў ╬и ­ЮњЪ , рхљrenРѓЎРѓў ╬и Рё░
  рхљrenРѓЎРѓў ╬и tt        = tt
  рхљrenРѓЎРѓў ╬и (Рїю ­ЮњЪ РїЮ)   = Рїю рхљren ╬и ­ЮњЪ РїЮ
  рхљrenРѓЎРѓў ╬и (Рїъ ­ЮњЪ РїЪ Рё░) = Рїъ рхљrenРѓЎРѓю ╬и ­ЮњЪ РїЪ (рхљrenРѓЎРѓў (lift ╬и) Рё░)
  рхљrenРѓЎРѓў ╬и (РЂ┐рхЌ ­ЮњЪ)    = РЂ┐рхЌ (рхљrenРѓЎРѓю ╬и ­ЮњЪ)

  рхљrenРѓЎРѓю : Рѕђ {╬ћ ╬ћРђ▓ ╬Њ A} Рєњ ╬ћРђ▓ РіЄ ╬ћ Рєњ ╬ћ РЂЈ ╬Њ РібРѓЎРѓю A
                        Рєњ ╬ћРђ▓ РЂЈ ╬Њ РібРѓЎРѓю A
  рхљrenРѓЎРѓю ╬и (рхљрхЏ i)  = рхљрхЏ (lookupрхБ ╬и i)
  рхљrenРѓЎРѓю ╬и (рхЏ i )  = рхЏ i
  рхљrenРѓЎРѓю ╬и (­ЮњЪ $ Рё░) = рхљrenРѓЎРѓю ╬и ­ЮњЪ $ рхљrenРѓЎРѓў ╬и Рё░
  рхљrenРѓЎРѓю ╬и (¤ђРѓЂ ­ЮњЪ)  = ¤ђРѓЂ (рхљrenРѓЎРѓю ╬и ­ЮњЪ)
  рхљrenРѓЎРѓю ╬и (¤ђРѓѓ ­ЮњЪ)  = ¤ђРѓѓ (рхљrenРѓЎРѓю ╬и ­ЮњЪ)


mutual
  renРѓЎРѓў : Рѕђ {╬ћ ╬Њ ╬ЊРђ▓ A} Рєњ ╬ЊРђ▓ РіЄ ╬Њ Рєњ ╬ћ РЂЈ ╬Њ РібРѓЎРѓў A
                       Рєњ ╬ћ РЂЈ ╬ЊРђ▓ РібРѓЎРѓў A
  renРѓЎРѓў ╬и (кЏ ­ЮњЪ)     = кЏ (renРѓЎРѓў (lift ╬и) ­ЮњЪ)
  renРѓЎРѓў ╬и (­ЮњЪ , Рё░)   = renРѓЎРѓў ╬и ­ЮњЪ , renРѓЎРѓў ╬и Рё░
  renРѓЎРѓў ╬и tt        = tt
  renРѓЎРѓў ╬и (Рїю ­ЮњЪ РїЮ)   = Рїю ­ЮњЪ РїЮ
  renРѓЎРѓў ╬и (Рїъ ­ЮњЪ РїЪ Рё░) = Рїъ renРѓЎРѓю ╬и ­ЮњЪ РїЪ (renРѓЎРѓў ╬и Рё░)
  renРѓЎРѓў ╬и (РЂ┐рхЌ ­ЮњЪ)    = РЂ┐рхЌ (renРѓЎРѓю ╬и ­ЮњЪ)

  renРѓЎРѓю : Рѕђ {╬ћ ╬Њ ╬ЊРђ▓ A} Рєњ ╬ЊРђ▓ РіЄ ╬Њ Рєњ ╬ћ РЂЈ ╬Њ РібРѓЎРѓю A
                       Рєњ ╬ћ РЂЈ ╬ЊРђ▓ РібРѓЎРѓю A
  renРѓЎРѓю ╬и (рхљрхЏ i)  = рхљрхЏ i
  renРѓЎРѓю ╬и (рхЏ i)   = рхЏ (lookupрхБ ╬и i)
  renРѓЎРѓю ╬и (­ЮњЪ $ Рё░) = renРѓЎРѓю ╬и ­ЮњЪ $ renРѓЎРѓў ╬и Рё░
  renРѓЎРѓю ╬и (¤ђРѓЂ ­ЮњЪ)  = ¤ђРѓЂ (renРѓЎРѓю ╬и ­ЮњЪ)
  renРѓЎРѓю ╬и (¤ђРѓѓ ­ЮњЪ)  = ¤ђРѓѓ (renРѓЎРѓю ╬и ­ЮњЪ)


renРѓЎРѓў┬▓ : Рѕђ {╬ћ ╬ћРђ▓ ╬Њ ╬ЊРђ▓ A} Рєњ ╬ћРђ▓ РЂЈ ╬ЊРђ▓ РіЄ┬▓ ╬ћ РЂЈ ╬Њ Рєњ ╬ћ РЂЈ ╬Њ РібРѓЎРѓў A
                         Рєњ ╬ћРђ▓ РЂЈ ╬ЊРђ▓ РібРѓЎРѓў A
renРѓЎРѓў┬▓ ╬и ­ЮњЪ = (рхљrenРѓЎРѓў (projРѓЂ ╬и) Рѕў renРѓЎРѓў (projРѓѓ ╬и)) ­ЮњЪ

renРѓЎРѓю┬▓ : Рѕђ {╬ћ ╬ћРђ▓ ╬Њ ╬ЊРђ▓ A} Рєњ ╬ћРђ▓ РЂЈ ╬ЊРђ▓ РіЄ┬▓ ╬ћ РЂЈ ╬Њ Рєњ ╬ћ РЂЈ ╬Њ РібРѓЎРѓю A
                         Рєњ ╬ћРђ▓ РЂЈ ╬ЊРђ▓ РібРѓЎРѓю A
renРѓЎРѓю┬▓ ╬и ­ЮњЪ = (рхљrenРѓЎРѓю (projРѓЂ ╬и) Рѕў renРѓЎРѓю (projРѓѓ ╬и)) ­ЮњЪ


--------------------------------------------------------------------------------
--
-- Substitution


-- Simultaneous substitutions
infix 3 _РібРІє_
_РібРІє_ : Cx Рєњ List Tp Рєњ Set
╬ћ РЂЈ ╬Њ РібРІє ╬ъ = All (╬ћ РЂЈ ╬Њ Ріб_) ╬ъ


рхљrenРІє : Рѕђ {╬ћ ╬ћРђ▓ ╬Њ ╬ъ} Рєњ ╬ћРђ▓ РіЄ ╬ћ Рєњ ╬ћ РЂЈ ╬Њ РібРІє ╬ъ
                     Рєњ ╬ћРђ▓ РЂЈ ╬Њ РібРІє ╬ъ
рхљrenРІє ╬и ¤Ѓ = mapРѓљ (рхљren ╬и) ¤Ѓ

renРІє : Рѕђ {╬ћ ╬Њ ╬ЊРђ▓ ╬ъ} Рєњ ╬ЊРђ▓ РіЄ ╬Њ Рєњ ╬ћ РЂЈ ╬Њ РібРІє ╬ъ
                     Рєњ ╬ћ РЂЈ ╬ЊРђ▓ РібРІє ╬ъ
renРІє ╬и ¤Ѓ = mapРѓљ (ren ╬и) ¤Ѓ


рхљwkРѓЏ : Рѕђ {╬ћ ╬Њ ╬ъ A} Рєњ ╬ћ РЂЈ ╬Њ РібРІє ╬ъ
                   Рєњ ╬ћ , A РЂЈ ╬Њ РібРІє ╬ъ
рхљwkРѓЏ ¤Ѓ = рхљrenРІє (wk idрхБ) ¤Ѓ

wkРѓЏ : Рѕђ {╬ћ ╬Њ ╬ъ A} Рєњ ╬ћ РЂЈ ╬Њ РібРІє ╬ъ
                  Рєњ ╬ћ РЂЈ ╬Њ , A РібРІє ╬ъ
wkРѓЏ ¤Ѓ = renРІє (wk idрхБ) ¤Ѓ

рхљliftРѓЏ : Рѕђ {╬ћ ╬Њ ╬ъ A} Рєњ ╬ћ РЂЈ ╬Њ РібРІє ╬ъ
                     Рєњ ╬ћ , A РЂЈ ╬Њ РібРІє ╬ъ , A
рхљliftРѓЏ ¤Ѓ = рхљwkРѓЏ ¤Ѓ , рхљрхЏ zero

liftРѓЏ : Рѕђ {╬ћ ╬Њ ╬ъ A} Рєњ ╬ћ РЂЈ ╬Њ РібРІє ╬ъ
                    Рєњ ╬ћ РЂЈ ╬Њ , A РібРІє ╬ъ , A
liftРѓЏ ¤Ѓ = wkРѓЏ ¤Ѓ , рхЏ zero


рхљidРѓЏ : Рѕђ {╬ћ ╬Њ} Рєњ ╬ћ РЂЈ ╬Њ РібРІє ╬ћ
рхљidРѓЏ {РѕЁ}     = РѕЁ
рхљidРѓЏ {╬Њ , A} = рхљliftРѓЏ рхљidРѓЏ

idРѓЏ : Рѕђ {╬Њ ╬ћ} Рєњ ╬ћ РЂЈ ╬Њ РібРІє ╬Њ
idРѓЏ {РѕЁ}     = РѕЁ
idРѓЏ {╬Њ , A} = liftРѓЏ idРѓЏ


lookupРѓЏ : Рѕђ {╬ћ ╬Њ ╬ъ A} Рєњ ╬ћ РЂЈ ╬Њ РібРІє ╬ъ Рєњ ╬ъ РѕІ A
                      Рєњ ╬ћ РЂЈ ╬Њ Ріб A
lookupРѓЏ ¤Ѓ i = lookupРѓљ ¤Ѓ i


-- Substitution
рхљsub : Рѕђ {╬ћ ╬Њ ╬ъ A} Рєњ ╬ћ РЂЈ РѕЁ РібРІє ╬ъ Рєњ ╬ъ РЂЈ ╬Њ Ріб A
                   Рєњ ╬ћ РЂЈ ╬Њ Ріб A
рхљsub ¤Ѓ (рхљрхЏ i)    = ren infрхБ (lookupРѓЏ ¤Ѓ i)
рхљsub ¤Ѓ (рхЏ i)     = рхЏ i
рхљsub ¤Ѓ (кЏ ­ЮњЪ)     = кЏ (рхљsub ¤Ѓ ­ЮњЪ)
рхљsub ¤Ѓ (­ЮњЪ $ Рё░)   = рхљsub ¤Ѓ ­ЮњЪ $ рхљsub ¤Ѓ Рё░
рхљsub ╬и (­ЮњЪ , Рё░)   = рхљsub ╬и ­ЮњЪ , рхљsub ╬и Рё░
рхљsub ╬и (¤ђРѓЂ ­ЮњЪ)    = ¤ђРѓЂ (рхљsub ╬и ­ЮњЪ)
рхљsub ╬и (¤ђРѓѓ ­ЮњЪ)    = ¤ђРѓѓ (рхљsub ╬и ­ЮњЪ)
рхљsub ╬и tt        = tt
рхљsub ¤Ѓ Рїю ­ЮњЪ РїЮ     = Рїю рхљsub ¤Ѓ ­ЮњЪ РїЮ
рхљsub ¤Ѓ (Рїъ ­ЮњЪ РїЪ Рё░) = Рїъ рхљsub ¤Ѓ ­ЮњЪ РїЪ (рхљsub (рхљliftРѓЏ ¤Ѓ) Рё░)

sub : Рѕђ {╬ћ ╬Њ ╬ъ A} Рєњ ╬ћ РЂЈ ╬Њ РібРІє ╬ъ Рєњ ╬ћ РЂЈ ╬ъ Ріб A
                  Рєњ ╬ћ РЂЈ ╬Њ Ріб A
sub ¤Ѓ (рхљрхЏ i)    = рхљрхЏ i
sub ¤Ѓ (рхЏ i)     = lookupРѓЏ ¤Ѓ i
sub ¤Ѓ (кЏ ­ЮњЪ)     = кЏ (sub (liftРѓЏ ¤Ѓ) ­ЮњЪ)
sub ¤Ѓ (­ЮњЪ $ Рё░)   = sub ¤Ѓ ­ЮњЪ $ sub ¤Ѓ Рё░
sub ╬и (­ЮњЪ , Рё░)   = sub ╬и ­ЮњЪ , sub ╬и Рё░
sub ╬и (¤ђРѓЂ ­ЮњЪ)    = ¤ђРѓЂ (sub ╬и ­ЮњЪ)
sub ╬и (¤ђРѓѓ ­ЮњЪ)    = ¤ђРѓѓ (sub ╬и ­ЮњЪ)
sub ╬и tt        = tt
sub ¤Ѓ Рїю ­ЮњЪ РїЮ     = Рїю ­ЮњЪ РїЮ
sub ¤Ѓ (Рїъ ­ЮњЪ РїЪ Рё░) = Рїъ sub ¤Ѓ ­ЮњЪ РїЪ (sub (рхљwkРѓЏ ¤Ѓ) Рё░)


--------------------------------------------------------------------------------
--
-- Semantics


-- Introspective Kripke models
record ­Юћј : SetРѓЂ where
  field
    ­Юњ▓    : Set

    _­Юњ▒_  : ­Юњ▓ Рєњ TVar Рєњ Set

    _РЅЦ_  : ­Юњ▓ Рєњ ­Юњ▓ Рєњ Set

    idРѓљ  : Рѕђ {w} Рєњ w РЅЦ w

    _РѕўРѓљ_ : Рѕђ {w wРђ▓ wРђ│} Рєњ wРђ▓ РЅЦ w Рєњ wРђ│ РЅЦ wРђ▓
                       Рєњ wРђ│ РЅЦ w

    movрхЦ : Рѕђ {x w wРђ▓} Рєњ wРђ▓ РЅЦ w Рєњ w ­Юњ▒ x
                      Рєњ wРђ▓ ­Юњ▒ x

    Рїі_РїІ  : ­Юњ▓ Рєњ Cx

    Рїі_РїІРѓљ : Рѕђ {w wРђ▓} Рєњ wРђ▓ РЅЦ w
                    Рєњ Рїі wРђ▓ РїІ РіЄ┬▓ Рїі w РїІ

open ­Юћј {{Рђд}} public


рхљРїі_РїІ : Рѕђ {{­Юћљ : ­Юћј}} Рєњ ­Юњ▓ Рєњ List Tp
рхљРїі w РїІ = projРѓЂ Рїі w РїІ

рхљРїі_РїІРѓљ : Рѕђ {{­Юћљ : ­Юћј}} {w wРђ▓} Рєњ wРђ▓ РЅЦ w
                           Рєњ рхљРїі wРђ▓ РїІ РіЄ рхљРїі w РїІ
рхљРїі ╬и РїІРѓљ = projРѓЂ Рїі ╬и РїІРѓљ


-- Values
mutual
  infix 3 _РіЕ_
  _РіЕ_ : Рѕђ {{­Юћљ : ­Юћј}} Рєњ ­Юњ▓ Рєњ Tp Рєњ Set

  w РіЕ рхЌрхЏ x  = w ­Юњ▒ x

  w РіЕ A РіЃ B = Рѕђ {wРђ▓} Рєњ (╬и : wРђ▓ РЅЦ w) (k : wРђ▓ рхЈРіЕ A)
                      Рєњ wРђ▓ рхЈРіЕ B

  w РіЕ A РѕД B = w рхЈРіЕ A ├Ќ w рхЈРіЕ B

  w РіЕ ­ЮћЌ     = Ріц

  w РіЕ РќА A   = w рхљрхЈРіЕ A

  infix 3 _рхЈРіЕ_
  _рхЈРіЕ_ : Рѕђ {{­Юћљ : ­Юћј}} Рєњ ­Юњ▓ Рєњ Tp Рєњ Set
  w рхЈРіЕ A = Рѕђ {wРђ▓ C} Рєњ (╬и : wРђ▓ РЅЦ w) (f : Рѕђ {wРђ│} Рєњ wРђ│ РЅЦ wРђ▓ Рєњ wРђ│ РіЕ A
                                                 Рєњ Рїі wРђ│ РїІ РібРѓЎРѓў C)
                     Рєњ Рїі wРђ▓ РїІ РібРѓЎРѓў C

  infix 3 _рхљрхЈРіЕ_
  _рхљрхЈРіЕ_ : Рѕђ {{­Юћљ : ­Юћј}} Рєњ ­Юњ▓ Рєњ Tp Рєњ Set
  w рхљрхЈРіЕ A = рхљРїі w РїІ РЂЈ РѕЁ Ріб A ├Ќ w рхЈРіЕ A


syn : Рѕђ {{­Юћљ : ­Юћј}} {w A} Рєњ w рхљрхЈРіЕ A
                        Рєњ рхљРїі w РїІ РЂЈ РѕЁ Ріб A
syn p = projРѓЂ p

sem : Рѕђ {{­Юћљ : ­Юћј}} {w A} Рєњ w рхљрхЈРіЕ A
                        Рєњ w рхЈРіЕ A
sem p = projРѓѓ p


-- Environments
infix 3 _рхЈРіЕРІє_
_рхЈРіЕРІє_ : Рѕђ {{­Юћљ : ­Юћј}} Рєњ ­Юњ▓ Рєњ List Tp Рєњ Set
w рхЈРіЕРІє ╬ъ = All (w рхЈРіЕ_) ╬ъ

infix 3 _рхљрхЈРіЕРІє_
_рхљрхЈРіЕРІє_ : Рѕђ {{­Юћљ : ­Юћј}} Рєњ ­Юњ▓ Рєњ List Tp Рєњ Set
w рхљрхЈРіЕРІє ╬ъ = All (w рхљрхЈРіЕ_) ╬ъ


synРІє : Рѕђ {{­Юћљ : ­Юћј}} {w ╬ъ} Рєњ w рхљрхЈРіЕРІє ╬ъ
                         Рєњ рхљРїі w РїІ РЂЈ РѕЁ РібРІє ╬ъ
synРІє ╬┤ = mapРѓљ syn ╬┤

semРІє : Рѕђ {{­Юћљ : ­Юћј}} {w ╬ъ} Рєњ w рхљрхЈРіЕРІє ╬ъ
                         Рєњ w рхЈРіЕРІє ╬ъ
semРІє ╬┤ = mapРѓљ sem ╬┤


-- Semantic entailment
infix 3 _Ріе_
_Ріе_ : Cx Рєњ Tp Рєњ SetРѓЂ
╬ћ РЂЈ ╬Њ Ріе A = Рѕђ {{­Юћљ : ­Юћј}} {w} Рєњ w рхљрхЈРіЕРІє ╬ћ Рєњ w рхЈРіЕРІє ╬Њ
                             Рєњ w рхЈРіЕ A


-- Accessibility
mutual
  mov : Рѕђ {{­Юћљ : ­Юћј}} {A w wРђ▓} Рєњ wРђ▓ РЅЦ w Рєњ w РіЕ A
                             Рєњ wРђ▓ РіЕ A
  mov {рхЌрхЏ x}  ╬и v = movрхЦ ╬и v
  mov {A РіЃ B} ╬и f = ╬╗ ╬иРђ▓ Рєњ f (╬и РѕўРѓљ ╬иРђ▓)
  mov {A РѕД B} ╬и p = рхЈmov {A} ╬и (projРѓЂ p) , рхЈmov {B} ╬и (projРѓѓ p)
  mov {­ЮћЌ}     ╬и t = tt
  mov {РќА A}   ╬и p = рхљрхЈmov ╬и p

  рхЈmov : Рѕђ {{­Юћљ : ­Юћј}} {A w wРђ▓} Рєњ wРђ▓ РЅЦ w Рєњ w рхЈРіЕ A
                              Рєњ wРђ▓ рхЈРіЕ A
  рхЈmov ╬и k = ╬╗ ╬иРђ▓ f Рєњ k (╬и РѕўРѓљ ╬иРђ▓) f

  рхљрхЈmov : Рѕђ {{­Юћљ : ­Юћј}} {A w wРђ▓} Рєњ wРђ▓ РЅЦ w Рєњ w рхљрхЈРіЕ A
                               Рєњ wРђ▓ рхљрхЈРіЕ A
  рхљрхЈmov {A} ╬и p = рхљren рхљРїі ╬и РїІРѓљ (syn p) , рхЈmov {A} ╬и (sem p)


рхЈmovРІє : Рѕђ {{­Юћљ : ­Юћј}} {╬ъ w wРђ▓} Рєњ wРђ▓ РЅЦ w Рєњ w рхЈРіЕРІє ╬ъ
                             Рєњ wРђ▓ рхЈРіЕРІє ╬ъ
-- TODO: Why does Agda require me to add seemingly unused annotations here?
-- рхЈmovРІє ╬и ╬│ = mapAll (рхЈmov ╬и) ╬│
рхЈmovРІє ╬и ╬│ = mapРѓљ (╬╗ {A} k {_} {_} Рєњ рхЈmov {A} ╬и (k {_})) ╬│

рхљрхЈmovРІє : Рѕђ {{­Юћљ : ­Юћј}} {╬ъ w wРђ▓} Рєњ wРђ▓ РЅЦ w Рєњ w рхљрхЈРіЕРІє ╬ъ
                              Рєњ wРђ▓ рхљрхЈРіЕРІє ╬ъ
рхљрхЈmovРІє ╬и ╬┤ = mapРѓљ (рхљрхЈmov ╬и) ╬┤


-- Kripke continuation monad
unit : Рѕђ {{­Юћљ : ­Юћј}} {A w} Рєњ w РіЕ A
                           Рєњ w рхЈРіЕ A
unit {A} a = ╬╗ ╬и f Рєњ
               f idРѓљ (mov {A} ╬и a)

bind : Рѕђ {{­Юћљ : ­Юћј}} {A C w} Рєњ w рхЈРіЕ A Рєњ (Рѕђ {wРђ▓} Рєњ wРђ▓ РЅЦ w Рєњ wРђ▓ РіЕ A
                                                 Рєњ wРђ▓ рхЈРіЕ C)
                           Рєњ w рхЈРіЕ C
bind k f = ╬╗ ╬и fРђ▓ Рєњ
             k ╬и (╬╗ ╬иРђ▓ a Рєњ
               f (╬и РѕўРѓљ ╬иРђ▓) a idРѓљ (╬╗ ╬иРђ│ b Рєњ
                 fРђ▓ (╬иРђ▓ РѕўРѓљ ╬иРђ│) b))


lookup : Рѕђ {{­Юћљ : ­Юћј}} {╬ъ A w} Рєњ w рхЈРіЕРІє ╬ъ Рєњ ╬ъ РѕІ A
                             Рєњ w рхЈРіЕ A
lookup ╬│ i = lookupРѓљ ╬│ i


-- Soundness
РєЊ : Рѕђ {╬ћ ╬Њ A} Рєњ ╬ћ РЂЈ ╬Њ Ріб A
              Рєњ ╬ћ РЂЈ ╬Њ Ріе A
РєЊ (рхљрхЏ i)            = ╬╗ ╬┤ ╬│ Рєњ lookup (semРІє ╬┤) i
РєЊ (рхЏ i)             = ╬╗ ╬┤ ╬│ Рєњ lookup ╬│ i
РєЊ (кЏ {A} {B} ­ЮњЪ)     = ╬╗ ╬┤ ╬│ Рєњ unit {A РіЃ B} (╬╗ ╬и k Рєњ
                        РєЊ ­ЮњЪ (рхљрхЈmovРІє ╬и ╬┤) (рхЈmovРІє ╬и ╬│ , k))
РєЊ (_$_ {A} {B} ­ЮњЪ Рё░) = ╬╗ ╬┤ ╬│ Рєњ bind {A РіЃ B} {B} (РєЊ ­ЮњЪ ╬┤ ╬│) (╬╗ ╬и f Рєњ
                        f idРѓљ (РєЊ Рё░ (рхљрхЈmovРІє ╬и ╬┤) (рхЈmovРІє ╬и ╬│)))
РєЊ (_,_ {A} {B} ­ЮњЪ Рё░) = ╬╗ ╬┤ ╬│ Рєњ unit {A РѕД B} (РєЊ ­ЮњЪ ╬┤ ╬│ , РєЊ Рё░ ╬┤ ╬│)
РєЊ (¤ђРѓЂ {A} {B} ­ЮњЪ)    = ╬╗ ╬┤ ╬│ Рєњ bind {A РѕД B} {A} (РєЊ ­ЮњЪ ╬┤ ╬│) (╬╗ ╬и p Рєњ
                        projРѓЂ p)
РєЊ (¤ђРѓѓ {A} {B} ­ЮњЪ)    = ╬╗ ╬┤ ╬│ Рєњ bind {A РѕД B} {B} (РєЊ ­ЮњЪ ╬┤ ╬│) (╬╗ ╬и p Рєњ
                        projРѓѓ p)
РєЊ tt                = ╬╗ ╬┤ ╬│ Рєњ unit {­ЮћЌ} tt
РєЊ (Рїю_РїЮ {A} ­ЮњЪ)       = ╬╗ ╬┤ ╬│ Рєњ unit {РќА A} (рхљsub (synРІє ╬┤) ­ЮњЪ , РєЊ ­ЮњЪ ╬┤ РѕЁ)
РєЊ (Рїъ_РїЪ {A} {C} ­ЮњЪ Рё░) = ╬╗ ╬┤ ╬│ Рєњ bind {РќА A} {C} (РєЊ ­ЮњЪ ╬┤ ╬│) (╬╗ ╬и p Рєњ
                        РєЊ Рё░ (рхљрхЈmovРІє ╬и ╬┤ , p) (рхЈmovРІє ╬и ╬│))


--------------------------------------------------------------------------------
--
-- Completeness


-- Universal model
instance
  ­Юћљрхц : ­Юћј
  ­Юћљрхц = record
         { ­Юњ▓    = Cx
         ; _­Юњ▒_  = _­Юњ▒рхц_
         ; _РЅЦ_  = _РіЄ┬▓_
         ; idРѓљ  = idрхБ┬▓
         ; _РѕўРѓљ_ = _РѕўрхБ┬▓_
         ; movрхЦ = renРѓЎРѓў┬▓
         ; Рїі_РїІ  = id
         ; Рїі_РїІРѓљ = id
         }
    where
      infix 3 _­Юњ▒рхц_
      _­Юњ▒рхц_ : Cx Рєњ TVar Рєњ Set
      ╬ћ РЂЈ ╬Њ ­Юњ▒рхц x = ╬ћ РЂЈ ╬Њ РібРѓЎРѓў рхЌрхЏ x


-- Soundness and completeness with respect to the universal model
mutual
  РєЊрхц : Рѕђ {A ╬ћ ╬Њ} Рєњ ╬ћ РЂЈ ╬Њ РібРѓЎРѓю A
                 Рєњ ╬ћ РЂЈ ╬Њ рхЈРіЕ A
  РєЊрхц {рхЌрхЏ x}  ­ЮњЪ = unit {рхЌрхЏ x} (РЂ┐рхЌ ­ЮњЪ)
  РєЊрхц {A РіЃ B} ­ЮњЪ = unit {A РіЃ B} (╬╗ ╬и k Рєњ РєЊрхц (renРѓЎРѓю┬▓ ╬и ­ЮњЪ $ РєЉрхц k))
  РєЊрхц {A РѕД B} ­ЮњЪ = unit {A РѕД B} (РєЊрхц (¤ђРѓЂ ­ЮњЪ) , РєЊрхц (¤ђРѓѓ ­ЮњЪ))
  РєЊрхц {­ЮћЌ}     ­ЮњЪ = unit {­ЮћЌ} tt
  РєЊрхц {РќА A}   ­ЮњЪ = ╬╗ ╬и f Рєњ Рїъ renРѓЎРѓю┬▓ ╬и ­ЮњЪ РїЪ (f (рхљwk┬▓ idрхБ┬▓) (рхљрхЏ zero , РєЊрхц (рхљрхЏ zero)))

  РєЉрхц : Рѕђ {A ╬ћ ╬Њ} Рєњ ╬ћ РЂЈ ╬Њ рхЈРіЕ A
                 Рєњ ╬ћ РЂЈ ╬Њ РібРѓЎРѓў A
  РєЉрхц {рхЌрхЏ x}  k = k idрхБ┬▓ (╬╗ ╬и v Рєњ v)
  РєЉрхц {A РіЃ B} k = k idрхБ┬▓ (╬╗ ╬и f Рєњ кЏ (РєЉрхц (f (wk┬▓ idрхБ┬▓) (РєЊрхц (рхЏ zero)))))
  РєЉрхц {A РѕД B} k = k idрхБ┬▓ (╬╗ ╬и p Рєњ РєЉрхц (projРѓЂ p) , РєЉрхц (projРѓѓ p))
  РєЉрхц {­ЮћЌ}     k = k idрхБ┬▓ (╬╗ ╬и t Рєњ tt)
  РєЉрхц {РќА A}   k = k idрхБ┬▓ (╬╗ ╬и p Рєњ Рїю syn p РїЮ)


рхљidРѓЉ : Рѕђ {╬ћ ╬Њ} Рєњ ╬ћ РЂЈ ╬Њ рхљрхЈРіЕРІє ╬ћ
рхљidРѓЉ {РѕЁ}     = РѕЁ
рхљidРѓЉ {╬ћ , A} = рхљрхЈmovРІє (рхљwk┬▓ idрхБ┬▓) рхљidРѓЉ , (рхљрхЏ zero , РєЊрхц (рхљрхЏ zero))

idРѓЉ : Рѕђ {╬Њ ╬ћ} Рєњ ╬ћ РЂЈ ╬Њ рхЈРіЕРІє ╬Њ
idРѓЉ {РѕЁ}     = РѕЁ
idРѓЉ {╬Њ , A} = рхЈmovРІє (wk┬▓ idрхБ┬▓) idРѓЉ , РєЊрхц (рхЏ zero)


-- Completeness
РєЉ : Рѕђ {╬ћ ╬Њ A} Рєњ ╬ћ РЂЈ ╬Њ Ріе A
              Рєњ ╬ћ РЂЈ ╬Њ РібРѓЎРѓў A
РєЉ f = РєЉрхц (f рхљidРѓЉ idРѓЉ)


-- Normalisation
nm : Рѕђ {╬ћ ╬Њ A} Рєњ ╬ћ РЂЈ ╬Њ Ріб A
               Рєњ ╬ћ РЂЈ ╬Њ РібРѓЎРѓў A
nm = РєЉ Рѕў РєЊ


--------------------------------------------------------------------------------
--
-- Examples


рхљрхЏ0 : Рѕђ {╬ћ ╬Њ A} Рєњ ╬ћ , A РЂЈ ╬Њ Ріб A
рхљрхЏ0 = рхљрхЏ zero

рхљрхЏ1 : Рѕђ {╬ћ ╬Њ A B} Рєњ ╬ћ , A , B РЂЈ ╬Њ Ріб A
рхљрхЏ1 = рхљрхЏ (suc zero)

рхљрхЏ2 : Рѕђ {╬ћ ╬Њ A B C} Рєњ ╬ћ , A , B , C РЂЈ ╬Њ Ріб A
рхљрхЏ2 = рхљрхЏ (suc (suc zero))


рхЏ0 : Рѕђ {╬ћ ╬Њ A} Рєњ ╬ћ РЂЈ ╬Њ , A Ріб A
рхЏ0 = рхЏ zero

рхЏ1 : Рѕђ {╬ћ ╬Њ A B} Рєњ ╬ћ РЂЈ ╬Њ , A , B Ріб A
рхЏ1 = рхЏ (suc zero)

рхЏ2 : Рѕђ {╬ћ ╬Њ A B C} Рєњ ╬ћ РЂЈ ╬Њ , A , B , C Ріб A
рхЏ2 = рхЏ (suc (suc zero))


рхЃ╦БI : Рѕђ {A ╬ћ ╬Њ} Рєњ ╬ћ РЂЈ ╬Њ Ріб A РіЃ A
рхЃ╦БI = кЏ рхЏ0

рхЃ╦БK : Рѕђ {A B ╬ћ ╬Њ} Рєњ ╬ћ РЂЈ ╬Њ Ріб A РіЃ B РіЃ A
рхЃ╦БK = кЏ (кЏ рхЏ1)

рхЃ╦БS : Рѕђ {A B C ╬ћ ╬Њ} Рєњ ╬ћ РЂЈ ╬Њ Ріб (A РіЃ B РіЃ C) РіЃ (A РіЃ B) РіЃ A РіЃ C
рхЃ╦БS = кЏ (кЏ (кЏ ((рхЏ2 $ рхЏ0) $ (рхЏ1 $ рхЏ0))))


рхЃ╦БD : Рѕђ {A B ╬ћ ╬Њ} Рєњ ╬ћ РЂЈ ╬Њ Ріб РќА (A РіЃ B) РіЃ РќА A РіЃ РќА B
рхЃ╦БD = кЏ (кЏ (Рїъ рхЏ1 РїЪ (Рїъ рхЏ0 РїЪ Рїю рхљрхЏ1 $ рхљрхЏ0 РїЮ)))

рхЃ╦БT : Рѕђ {A ╬ћ ╬Њ} Рєњ ╬ћ РЂЈ ╬Њ Ріб РќА A РіЃ A
рхЃ╦БT = кЏ (Рїъ рхЏ0 РїЪ рхљрхЏ0)

рхЃ╦Б4 : Рѕђ {A ╬ћ ╬Њ} Рєњ ╬ћ РЂЈ ╬Њ Ріб РќА A РіЃ РќА РќА A
рхЃ╦Б4 = кЏ (Рїъ рхЏ0 РїЪ Рїю Рїю рхљрхЏ0 РїЮ РїЮ)


--------------------------------------------------------------------------------
--
-- Conversion tests


testРѕ╝ : Рѕђ {╬ћ ╬Њ A} Рєњ ╬ћ РЂЈ ╬Њ Ріб A Рєњ ╬ћ РЂЈ ╬Њ Ріб A Рєњ Set
testРѕ╝ ­ЮњЪРѓЂ ­ЮњЪРѓѓ = embРѓЎРѓў (nm ­ЮњЪРѓЂ) РЅА ­ЮњЪРѓѓ


-- redРіЃ : Рѕђ {╬ћ ╬Њ A B} Рєњ (­ЮњЪ : ╬ћ РЂЈ ╬Њ , A Ріб B) (Рё░ : ╬ћ РЂЈ ╬Њ Ріб A)
--                    Рєњ кЏ ­ЮњЪ $ Рё░ Рѕ╝ sub (idРѓЏ , Рё░) ­ЮњЪ

testРѕ╝redРіЃ : testРѕ╝ {РѕЁ} {РѕЁ , "A"}
                  ((кЏ рхЏ0) $ рхЏ0)
                  рхЏ0
testРѕ╝redРіЃ = refl


-- redРѕДРѓЂ : Рѕђ {╬ћ ╬Њ A B} Рєњ (­ЮњЪ : ╬ћ РЂЈ ╬Њ Ріб A) (Рё░ : ╬ћ РЂЈ ╬Њ Ріб B)
--                     Рєњ ¤ђРѓЂ (­ЮњЪ , Рё░) Рѕ╝ ­ЮњЪ

testРѕ╝redРѕДРѓЂ : testРѕ╝ {РѕЁ} {РѕЁ , "A" , "B"}
                   (¤ђРѓЂ (рхЏ1 , рхЏ0))
                   рхЏ1
testРѕ╝redРѕДРѓЂ = refl


-- redРѕДРѓѓ : Рѕђ {╬ћ ╬Њ A B} Рєњ (­ЮњЪ : ╬ћ РЂЈ ╬Њ Ріб A) (Рё░ : ╬ћ РЂЈ ╬Њ Ріб B)
--                     Рєњ ¤ђРѓѓ (­ЮњЪ , Рё░) Рѕ╝ Рё░

testРѕ╝redРѕДРѓѓ : testРѕ╝ {РѕЁ} {РѕЁ , "A" , "B"}
                   (¤ђРѓѓ (рхЏ1 , рхЏ0))
                   рхЏ0
testРѕ╝redРѕДРѓѓ = refl


-- redРќА : Рѕђ {╬ћ ╬Њ A C} Рєњ (­ЮњЪ : ╬ћ РЂЈ РѕЁ Ріб A) (Рё░ : ╬ћ , A РЂЈ ╬Њ Ріб C)
--                    Рєњ Рїъ Рїю ­ЮњЪ РїЮ РїЪ Рё░ Рѕ╝ рхљsub (рхљidРѓЏ , ­ЮњЪ) Рё░

testРѕ╝redРќА : testРѕ╝ {РѕЁ , "A"} {РѕЁ}
                  (Рїъ Рїю рхљрхЏ0 РїЮ РїЪ рхљрхЏ0)
                  рхљрхЏ0
testРѕ╝redРќА = refl


-- expРіЃ : Рѕђ {╬ћ ╬Њ A B} Рєњ (­ЮњЪ : ╬ћ РЂЈ ╬Њ Ріб A РіЃ B)
--                    Рєњ ­ЮњЪ Рѕ╝ кЏ (ren (wk idрхБ) ­ЮњЪ $ рхЏ0)

testРѕ╝expРіЃ : testРѕ╝ {РѕЁ} {РѕЁ , "A" РіЃ "B"}
                  рхЏ0
                  (кЏ (ren (wk idрхБ) рхЏ0 $ рхЏ0))
testРѕ╝expРіЃ = refl


-- expРѕД : Рѕђ {╬ћ ╬Њ A B} Рєњ (­ЮњЪ : ╬ћ РЂЈ ╬Њ Ріб A РѕД B)
--                    Рєњ ­ЮњЪ Рѕ╝ ¤ђРѓЂ ­ЮњЪ , ¤ђРѓѓ ­ЮњЪ

testРѕ╝expРѕД : testРѕ╝ {РѕЁ} {РѕЁ , "A" РѕД "B"}
                  рхЏ0
                  (¤ђРѓЂ рхЏ0 , ¤ђРѓѓ рхЏ0)
testРѕ╝expРѕД = refl


-- exp­ЮћЌ : Рѕђ {╬ћ ╬Њ} Рєњ (­ЮњЪ : ╬ћ РЂЈ ╬Њ Ріб ­ЮћЌ)
--                Рєњ ­ЮњЪ Рѕ╝ tt

testРѕ╝exp­ЮћЌ : testРѕ╝ {РѕЁ} {РѕЁ , ­ЮћЌ}
                  рхЏ0
                  tt
testРѕ╝exp­ЮћЌ = refl


-- expРќА : Рѕђ {╬ћ ╬Њ} Рєњ (­ЮњЪ : ╬ћ РЂЈ ╬Њ Ріб РќА A)
--                Рєњ ­ЮњЪ Рѕ╝ Рїъ ­ЮњЪ РїЪ Рїю рхљрхЏ0 РїЮ

testРѕ╝expРќА : testРѕ╝ {РѕЁ} {РѕЁ , РќА "A"}
                  рхЏ0
                  (Рїъ рхЏ0 РїЪ Рїю рхљрхЏ0 РїЮ)
testРѕ╝expРќА = refl


-- commРќА$ : Рѕђ {╬ћ ╬Њ A B} Рєњ (­Юњг : ╬ћ РЂЈ ╬Њ Ріб РќА (A РіЃ B))
--                         (­ЮњЪ : ╬ћ , A РіЃ B РЂЈ ╬Њ Ріб A РіЃ B) (Рё░ : ╬ћ РЂЈ ╬Њ Ріб A)
--                      Рєњ (Рїъ ­Юњг РїЪ ­ЮњЪ) $ Рё░ Рѕ╝ Рїъ ­Юњг РїЪ (­ЮњЪ $ (рхљren (wk idрхБ) Рё░))

testРѕ╝commРќА$ : testРѕ╝ {РѕЁ} {РѕЁ , РќА ("A" РіЃ "B") , "A"}
                    ((Рїъ рхЏ1 РїЪ рхљрхЏ0) $ рхЏ0)
                    (Рїъ рхЏ1 РїЪ (рхљрхЏ0 $ рхљren (wk idрхБ) рхЏ0))
testРѕ╝commРќА$ = refl


-- commРќАРїъРїЪ : Рѕђ {╬ћ ╬Њ A C} Рєњ (­Юњг : ╬ћ РЂЈ ╬Њ Ріб РќА РќА A)
--                          (­ЮњЪ : ╬ћ , РќА A РЂЈ ╬Њ Ріб РќА A) (Рё░ : ╬ћ , A РЂЈ ╬Њ Ріб C)
--                       Рєњ Рїъ (Рїъ ­Юњг РїЪ ­ЮњЪ) РїЪ Рё░ Рѕ╝ Рїъ ­Юњг РїЪ (Рїъ ­ЮњЪ РїЪ (рхљren (wk idрхБ) Рё░))

testРѕ╝commРќАРїъРїЪ : testРѕ╝ {РѕЁ} {РѕЁ , РќА РќА "A"}
                     (Рїъ Рїъ рхЏ0 РїЪ рхљрхЏ0 РїЪ рхљрхЏ0)
                     (Рїъ рхЏ0 РїЪ (Рїъ рхљрхЏ0 РїЪ рхљрхЏ0))
testРѕ╝commРќАРїъРїЪ = refl


-- commРќА¤ђРѓЂ : Рѕђ {╬ћ ╬Њ A B} Рєњ (­Юњг : ╬ћ РЂЈ ╬Њ Ріб РќА (A РѕД B))
--                          (­ЮњЪ : ╬ћ , A РѕД B РЂЈ ╬Њ Ріб A РѕД B)
--                       Рєњ ¤ђРѓЂ (Рїъ ­Юњг РїЪ ­ЮњЪ) Рѕ╝ Рїъ ­Юњг РїЪ (¤ђРѓЂ ­ЮњЪ)

testРѕ╝commРќА¤ђРѓЂ : testРѕ╝ {РѕЁ} {РѕЁ , РќА ("A" РѕД "B")}
                     (¤ђРѓЂ (Рїъ рхЏ0 РїЪ рхљрхЏ0))
                     (Рїъ рхЏ0 РїЪ (¤ђРѓЂ рхљрхЏ0))
testРѕ╝commРќА¤ђРѓЂ = refl


-- commРќА¤ђРѓѓ : Рѕђ {╬ћ ╬Њ A B} Рєњ (­Юњг : ╬ћ РЂЈ ╬Њ Ріб РќА (A РѕД B))
--                          (­ЮњЪ : ╬ћ , A РѕД B РЂЈ ╬Њ Ріб A РѕД B)
--                       Рєњ ¤ђРѓѓ (Рїъ ­Юњг РїЪ ­ЮњЪ) Рѕ╝ Рїъ ­Юњг РїЪ (¤ђРѓѓ ­ЮњЪ)

testРѕ╝commРќА¤ђРѓѓ : testРѕ╝ {РѕЁ} {РѕЁ , РќА ("A" РѕД "B")}
                     (¤ђРѓѓ (Рїъ рхЏ0 РїЪ рхљрхЏ0))
                     (Рїъ рхЏ0 РїЪ (¤ђРѓѓ рхљрхЏ0))
testРѕ╝commРќА¤ђРѓѓ = refl


--------------------------------------------------------------------------------
--
-- Self-interpretation


-- weakBP : Рѕђ {╬ћ ╬Њ A B} Рєњ (­ЮњЪ : ╬ћ РЂЈ ╬Њ Ріб A)
--                      Рєњ ­ЮћЌ $ Рїю ­ЮњЪ РїЮ Рѕ╝ Рїю ­ЮњЪ РїЮ

testРѕ╝weakBP : testРѕ╝ {РѕЁ , "A"} {РѕЁ}
                    (рхЃ╦БT $ Рїю рхљрхЏ0 РїЮ)
                    рхљрхЏ0
testРѕ╝weakBP = refl


-- Andrej : Рѕђ {╬ћ ╬Њ A B} Рєњ (­ЮњЪ : ╬ћ РЂЈ ╬Њ Ріб A РіЃ B) (Рё░ : ╬ћ РЂЈ ╬Њ Ріб A)
--                      Рєњ (­Юћ╗ $ Рїю ­ЮњЪ РїЮ) $ Рїю Рё░ РїЮ Рѕ╝ Рїю ­ЮњЪ $ Рё░ РїЮ

testРѕ╝Andrej : testРѕ╝ {РѕЁ , "A" РіЃ "B" , "A"} {РѕЁ}
                    ((рхЃ╦БD $ Рїю рхљрхЏ1 РїЮ) $ Рїю рхљрхЏ0 РїЮ)
                    (Рїю рхљрхЏ1 $ рхљрхЏ0 РїЮ)
testРѕ╝Andrej = refl


--------------------------------------------------------------------------------
